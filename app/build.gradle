apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.tcorner.trappic"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            resValue "string", "GOOGLE_MAPS_KEY", getReleaseApiKey()
            buildConfigField "String", "GOOGLE_MAPS_KEY", "\"" + getReleaseApiKey() + "\""
        }

        debug {
            resValue "string", "GOOGLE_MAPS_KEY", getDebugApiKey()
            buildConfigField "String", "GOOGLE_MAPS_KEY", "\"" + getDebugApiKey() + "\""
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation "com.android.support:appcompat-v7:$project.ext.supportVersion"
    implementation "com.android.support:design:$project.ext.supportVersion"
    implementation "com.android.support:animated-vector-drawable:$project.ext.supportVersion"
    implementation "com.android.support:support-v4:$project.ext.supportVersion"
    implementation "com.android.support:support-media-compat:$project.ext.supportVersion"
    implementation "com.android.support:support-vector-drawable:$project.ext.supportVersion"

    implementation "com.google.android.gms:play-services-maps:$project.ext.googleVersion"

    kapt "com.google.dagger:dagger-compiler:$project.ext.daggerVersion"
    implementation "com.google.dagger:dagger:$project.ext.daggerVersion"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    implementation "com.squareup.retrofit2:retrofit:$project.ext.retrofitVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$project.ext.retrofitVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$project.ext.okhttpVersion"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$project.ext.coroutinesVersion"

    implementation "android.arch.lifecycle:extensions:$project.ext.archComponentsVersion"
}

/**
 * include your google maps API key on local.properties
 *
 * @return debug google maps api key
 */
def getDebugApiKey() {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    return properties.getProperty("debug_google_maps_key")
}

/**
 * include your google maps API key on local.properties
 *
 * @return release google maps api key
 */
def getReleaseApiKey() {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    return properties.getProperty("release_google_maps_key")
}